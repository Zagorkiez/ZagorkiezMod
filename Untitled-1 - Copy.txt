class SCR_PlayerRadioSpawnPointClass: SCR_PlayerSpawnPointClass
{
};

class SCR_PlayerRadioSpawnPoint: SCR_PlayerSpawnPoint
{
	protected void OnItemAdded(IEntity item, BaseInventoryStorageComponent storageOwner)
	{
		BaseLoadoutClothComponent loadoutCloth = BaseLoadoutClothComponent.Cast(item.FindComponent(BaseLoadoutClothComponent));
		if (loadoutCloth && loadoutCloth.GetAreaType() && loadoutCloth.GetAreaType().IsInherited(LoadoutBackpackArea) && item.FindComponent(SCR_RadioComponent))
		{
			SCR_ChimeraCharacter chimeraCharacter = SCR_ChimeraCharacter.Cast(m_TargetPlayer);
			if (chimeraCharacter && IsGroupLeader(chimeraCharacter))
				super.ActivateSpawnPoint();
		}
	}

	protected void OnItemRemoved(IEntity item, BaseInventoryStorageComponent storageOwner)
	{
		BaseLoadoutClothComponent loadoutCloth = BaseLoadoutClothComponent.Cast(item.FindComponent(BaseLoadoutClothComponent));
		if (loadoutCloth && loadoutCloth.GetAreaType() && loadoutCloth.GetAreaType().IsInherited(LoadoutBackpackArea) && item.FindComponent(SCR_RadioComponent))
			super.DeactivateSpawnPoint();
	}

	override protected void ActivateSpawnPoint()
	{
		SCR_InventoryStorageManagerComponent inventoryManager = SCR_InventoryStorageManagerComponent.Cast(m_TargetPlayer.FindComponent(SCR_InventoryStorageManagerComponent));
		if (inventoryManager)
		{
			inventoryManager.m_OnItemAddedInvoker.Insert(OnItemAdded);
			inventoryManager.m_OnItemRemovedInvoker.Insert(OnItemRemoved);
		}

		EquipedLoadoutStorageComponent loadoutStorage = EquipedLoadoutStorageComponent.Cast(m_TargetPlayer.FindComponent(EquipedLoadoutStorageComponent));
		if (loadoutStorage)
		{
			IEntity backpack = loadoutStorage.GetClothFromArea(LoadoutBackpackArea);
			if (backpack)
				OnItemAdded(backpack, null);
		}
	}

	override protected void DeactivateSpawnPoint()
	{
		if (m_TargetPlayer)
		{
			SCR_InventoryStorageManagerComponent inventoryManager = SCR_InventoryStorageManagerComponent.Cast(m_TargetPlayer.FindComponent(SCR_InventoryStorageManagerComponent));
			if (inventoryManager)
			{
				inventoryManager.m_OnItemAddedInvoker.Remove(OnItemAdded);
				inventoryManager.m_OnItemRemovedInvoker.Remove(OnItemRemoved);
			}
		}
		super.DeactivateSpawnPoint();
	}

	bool IsGroupLeader(SCR_ChimeraCharacter player)
	{
		SCR_PlayerControllerGroupComponent groupController = SCR_PlayerControllerGroupComponent.GetLocalPlayerControllerGroupComponent();
		if (groupController)
		{
			return groupController.IsPlayerLeaderOwnGroup();
		}
		return false;
	}
};
